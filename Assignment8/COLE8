COLE8    START
*
*        JONATHON COLE MARLAR
*        ASSIGNMENT #4
*        21 MARCH 2014
*
*        INPUT:     A FILE OF UP TO 30 LINES, EACH LINE CONTAINING
*                   ONE INTEGER VALUE
*        EXECUTION: INTEGERS IN THE RANGE OF 0-100 WILL BE STORED IN
*                   A TABLE; THE TABLE WILL BE PRINTED AS IS; THE
*                   TABLE WILL BE SORTED AND PRINTED AGAIN
*        OUTPUT:    THE UNSORTED AND SORTED TABLE
*
*        BEGINNING HOUSEKEEPING
         STM   14,12,12(13)
         BALR  12,0
         USING HERE,12
HERE     ST    13,SAVEAREA+4
         LA    13,SAVEAREA
*
*        MAIN PROGRAM
*        REG KEY:
*        REG 1 : POINTS TO THE PARAMETER LIST TO PASS TO EXTERNAL
*                SUBROUTINES
*        REG 4 : THE INCREMENT SIZE USED TO MOVE THROUGH TABLE;
*                WILL NOT BE AFFECTED BY ANY SUBROUTINE
*        REG 14: USED TO CALL EXTERNAL SUBROUTINES
*        REG 15: USED TO CALL EXTERNAL SUBROUTINES
*
         LA    4,4
*        READ THE FILE
         LA    1,PARMLIST
         L     15,=V(READFILE)
         BALR  14,15
*        PRINT THE TABLE
         LA    1,PARMLIST
         L     15,=V(PRNTABLE)
         BALR  14,15
*        SORT THE TABLE
         LA    1,PARMLIST
         L     15,=V(SORTABLE)
         BALR  14,15
*        PRINT THE TABLE AGAIN
         LA    1,PARMLIST
         L     15,=V(PRNTABLE)
         BALR  14,15
*
*        END HOUSEKEEPING
         L     13,SAVEAREA+4
         LM    14,12,12(13)
         BR    14
SAVEAREA DS    18F
*
*        DECLARE STORAGE AND CONSTANTS
PRETABLE DS    1F
TABLE    DS    30F
INCVAL   DS    1F
*        PARAMTER LIST TO PASS
PARMLIST DC    A(PRETABLE)
         DC    A(TABLE)
         DC    A(INCVAL)
         LTORG
         EJECT
         TITLE 'READFILE SUBROUTINE'
*
*        DEFINE SUBROUTINES
*
*        READFILE
*        PURPOSE: READ IN ONE INTEGER PER LINE FROM INPUT AND,
*                 IF IN RANGE, STORES IT IN TABLE
*
*        REG KEY:
*        REG 3 : POINTS TO TABLE; INCREMENTS THROUGH TABLE AS
*                DATA IS SAVED
*        REG 5 : POINTS TO END OF TABLE; INCREMENTS AS DATA IS SAVED
*        REG 6 : USED TO HOLD THE NUMBER OF FULLWORDS 
*                NEEDED TO MOVE TO THE END OF THE TABLE;
*                WILL BE PASSED BACK INTO INCVAL
*        REG 8 : HOLDS TOTAL NUMBER OF LINES ALLOWED; USED AS LOOP
*                CONTROL
*        REG 9 : HOLDS VALUE OF 0 FOR COMPARISON
*        REG10 : HOLDS VALUE OF 100 FOR COMPARISON
*
READFILE CSECT
*        BEGINNING HOUSEKEEPING
         STM   14,12,12(13)
         BALR  12,0
         USING RDHERE,12
RDHERE   ST    13,RDSAVE+4
         LA    13,RDSAVE
*        GET PARAMETER LIST FROM REG 1
         L     5,0(1)
         L     3,4(1)
         L     6,8(1)
*        SET UP REGISTERS 8, 9, AND 10
         LA    8,30
         LA    9,0
         LA    10,100
GETLINE  XREAD CARD,80
*        IF EARLY EOF, BRANCH TO END
         BC    B'0100',EARLYOUT
         XDECI 2,CARD
*        COMPARE TO 0
         CR    2,9
         BC    B'0100',COMPLETE
*        COMPARE TO 100
         CR    2,10
         BC    B'0010',COMPLETE
*        THE NUMBER IS IN RANGE, STORE IT
         ST    2,0(3)
         LA    3,4(3)
         LA    5,4(5)
         ST    5,0(6)
*        ONCE IT'S STORED (OR DROPPED), LOOP BACK UP
COMPLETE BCT   8,GETLINE
*        ENDING HOUSEKEEPING
EARLYOUT L     13,RDSAVE+4
         LM    14,12,12(13)
         BR    14
RDSAVE   DS    18F
*        LOCAL DS & DC
CARD     DS    CL80
         LTORG
*        END OF READFILE
*
         EJECT
         TITLE 'PRNTABLE SUBROUTINE'
*        PRNTABLE
*        PURPOSE: PRINTS THE TABLE OUT IN A LIST FORMAT; SKIPS
*                 A PAGE WHEN CALLED TO PRINT
*
*        REG KEY:
*        REG 1 : USED TO GET PARAMETERS PASSED INTO SUBROUTINE
*        REG 3 : POINTS TO TABLE; INCREMENTS TO MOVE THROUGH TABLE
*        REG 4 : INCREMENTAL REGISTER; PART OF EVEN-ODD PAIR
*        REG 5 : POINTS TO END OF TABLE
*
PRNTABLE CSECT
*        BEGINNING HOUSEKEEPING
         STM   14,12,12(13)
         BALR  12,0
         USING PRTHERE,12
PRTHERE  ST    13,PRTSAVE+4
         LA    13,PRTSAVE
*
*        GET DATA FROM PARAMETER LIST
         L     3,4(1)
         L     5,8(1)
         L     5,0(5)
         XPRNT =C'1TABLE:',7
PRNTROW  L     2,0(3)
         XDECO 2,TBLEROW
         XPRNT TBLEOUT,13
         BXLE  3,4,PRNTROW
*        END HOUSEKEEPING
         L     13,PRTSAVE+4
         LM    14,12,12(13)
         BR    14
PRTSAVE  DS    18F
*        LOCAL DS & DC
TBLEOUT  DC    C' '
TBLEROW  DS    CL12
         LTORG
*        END OF PRNTABLE
*
         EJECT
         TITLE 'SORTABLE SUBROUTINE'
*        SORTABLE
*        PURPOSE: SORTS THE TABLE USING A SELECTION SORT
*                 FROM SMALLEST TO LARGEST INTEGER
*
*        REG KEY:
*        REG 1 : USED TO GET DATA FROM PARAMETER LIST PASSED INTO
*                SUBROUTINE
*        REG 3 : POINTS TO TABLE; INCREMENTS AS TABLE GETS SORTED
*        REG 4 : INCREMENT AMOUNT
*        REG 5 : POINTS TO END OF TABLE
*        REG 6 : HOLDS THE ADDRESSES OF THE TABLE CELLS AS THE SORT
*                PROGRESSES THROUGH THE TABLE
*        REG 7 : HOLDS THE DATA FOUND AT THE ADDRESS HELD BY REG 6
*        REG 8 : STORES THE ADDRESS THAT HELD THE SMALLEST VALUE
*        REG 9 : HOLDS A COPY OF THE DATA STORED AT ADDRESS IN REG 3
*        REG10 : HOLDS THE SMALLEST INTEGER VALUE
*
SORTABLE CSECT
*        BEGINNING HOUSEKEEPING
         STM   14,12,12(13)
         BALR  12,0
         USING SORTHERE,12
SORTHERE ST    13,SORTSAVE+4
         LA    13,SORTSAVE
*
*        GET DATA FROM PARAMETER LIST
         L     3,4(1)
         L     5,8(1)
         L     5,0(5)
*        RESET REG 10 TO MAX POSSIBLE VALUE FOR RANGE
STRTSRCH LA    10,999
         LR    6,3
THRUTABL L     7,0(6)
         CR    7,10
*        IGNORE THE VALUE IF IT IS NOT SMALLER
         BC    B'1011',INCREM
*        OTHERWISE, NOTE THE VALUE AND ADDRESS
         LR    10,7
         LR    8,6
INCREM   LA    6,4(6)
         CR    6,5
*        BRANCH IF WE'RE AT THE END OF THE TABLE
         BC    B'1101',THRUTABL
         L     9,0(3)
         ST    10,0(3)
         ST    9,0(8)
         BXLE  3,4,STRTSRCH
*        END HOUSEKEEPING
         L     13,SORTSAVE+4
         LM    14,12,12(13)
         BR    14
SORTSAVE DS    18F
         LTORG
*        END OF SORTABLE
         END   COLE8
$ENTRY NUMS.TXT
