COLE4    START
*
*        JONATHON COLE MARLAR
*        ASSIGNMENT #4
*        21 MARCH 2014
*
*        INPUT:     A FILE OF UP TO 30 LINES, EACH LINE CONTAINING
*                   ONE INTEGER VALUE
*        EXECUTION: INTEGERS IN THE RANGE OF 0-100 WILL BE STORED IN
*                   A TABLE; THE TABLE WILL BE PRINTED AS IS; THE
*                   TABLE WILL BE SORTED AND PRINTED AGAIN
*        OUTPUT:    THE UNSORTED AND SORTED TABLE
*
*        BEGINNING HOUSEKEEPING
         STM   14,12,12(13)
         BALR  12,0
         USING HERE,12
HERE     ST    13,SAVEAREA+4
         LA    13,SAVEAREA
*
*        MAIN PROGRAM
*        REG KEY:
*        REG 3 : POINTS TO TABLE; SUBROUTINES WILL NEED TO RESET
*                TO DEFAULT VALUE = TABLE
*        REG 4 : THE INCREMENT SIZE USED TO MOVE THROUGH TABLE;
*                WILL NOT BE AFFECTED BY ANY SUBROUTINE
*        REG 5 : POINTS TO THE END OF TABLE; DEFAULT = TABLE-4
*        REG11 : SUBROUTINE REGISTER; NOT TO BE EDITED
*
         LA    3,TABLE
         LA    4,4
         LA    5,TABLE-4
         BAL   11,READFILE
         BAL   11,PRNTABLE
         BAL   11,SORTABLE
         BAL   11,PRNTABLE
*
*        END HOUSEKEEPING
         L     13,SAVEAREA+4
         LM    14,12,12(13)
         BR    14
SAVEAREA DS    18F
*
*        DECLARE STORAGE AND CONSTANTS
CARD     DS    CL80
TABLE    DS    30F
TBLEOUT  DC    C' '
TBLEROW  DS    CL12
         TITLE 'READFILE SUBROUTINE'
*
*        DEFINE SUBROUTINES
*
*        READFILE
*        PURPOSE: READ IN ONE INTEGER PER LINE FROM INPUT AND,
*                 IF IN RANGE, STORES IT IN TABLE
*
*        REG KEY:
*        REG 3 : POINTS TO TABLE; INCREMENTS THROUGH TABLE AS
*                DATA IS SAVED
*        REG 5 : POINTS TO END OF TABLE; INCREMENTS AS DATA IS SAVED
*        REG 8 : HOLDS TOTAL NUMBER OF LINES ALLOWED; USED AS LOOP
*                CONTROL
*        REG 9 : HOLDS VALUE OF 0 FOR COMPARISON
*        REG10 : HOLDS VALUE OF 100 FOR COMPARISON
*
READFILE LA    8,30
         LA    9,0
         LA    10,100
GETLINE  XREAD CARD,80
*        IF EARLY EOF, BRANCH TO END
         BC    B'0100',EARLYOUT
         XDECI 2,CARD
*        COMPARE TO 0
         CR    2,9
         BC    B'0100',COMPLETE
*        COMPARE TO 100
         CR    2,10
         BC    B'0010',COMPLETE
*        THE NUMBER IS IN RANGE, STORE IT
         ST    2,0(3)
         LA    3,4(3)
         LA    5,4(5)
*        ONCE IT'S STORED (OR DROPPED), LOOP BACK UP
COMPLETE BCT   8,GETLINE
EARLYOUT BR    11
*        END OF READFILE
*
         TITLE 'PRNTABLE SUBROUTINE'
*        PRNTABLE
*        PURPOSE: PRINTS THE TABLE
*
*        REG KEY:
*        REG 3 : POINTS TO TABLE; INCREMENTS TO MOVE THROUGH TABLE
*        REG 4 : INCREMENTAL REGISTER; PART OF EVEN-ODD PAIR
*        REG 5 : POINTS TO END OF TABLE    
*
PRNTABLE LA    3,TABLE
         XPRNT =C'0TABLE:',7
PRNTROW  L     2,0(3)
         XDECO 2,TBLEROW
         XPRNT TBLEOUT,13
         BXLE  3,4,PRNTROW
         BR    11
*        END OF PRNTABLE
*
         TITLE 'SORTABLE SUBROUTINE'
*        SORTABLE
*        PURPOSE: SORTS THE TABLE FROM SMALLEST TO LARGEST INTEGER
*
*        REG KEY:
*        REG 3 : POINTS TO TABLE; INCREMENTS AS TABLE GETS SORTED
*        REG 4 : INCREMENT AMOUNT
*        REG 5 : POINTS TO END OF TABLE
*        REG 6 : HOLDS THE ADDRESSES OF THE TABLE CELLS AS THE SORT
*                PROGRESSES THROUGH THE TABLE
*        REG 7 : HOLDS THE DATA FOUND AT THE ADDRESS HELD BY REG 6
*        REG 8 : STORES THE ADDRESS THAT HELD THE SMALLEST VALUE
*        REG 9 : HOLDS A COPY OF THE DATA STORED AT ADDRESS IN REG 3
*        REG10 : HOLDS THE SMALLEST INTEGER VALUE
*
SORTABLE LA    3,TABLE
*        RESET REG 10 TO MAX POSSIBLE VALUE FOR RANGE
STRTSRCH LA    10,999
         LR    6,3
THRUTABL L     7,0(6)
         CR    7,10
*        IGNORE THE VALUE IF IT IS NOT SMALLER
         BC    B'1011',INCREM
*        OTHERWISE, NOTE THE VALUE AND ADDRESS
         LR    10,7
         LR    8,6
INCREM   LA    6,4(6)
         CR    6,5
*        BRANCH IF WE'RE AT THE END OF THE TABLE
         BC    B'1101',THRUTABL
         L     9,0(3)
         ST    10,0(3)
         ST    9,0(8)
         BXLE  3,4,STRTSRCH
         BR    11
*        END OF SORTABLE
         END   COLE4
$ENTRY NUMS.TXT
