COLE7    START
*********************************************************************
*
*        JONATHON COLE MARLAR
*        ASSIGNMENT 7
*        23 APRIL 2014
*
*        SAVINGS ACCOUNT
*        INPUT:    A FILE CONTAINING NUMERIC DATA IN COLUMNS 1-10
*        EXECUTE:  CALCULATES INITIAL BALANCE, DEPOSITS, WITHDRAWALS,
*                  OR INTEREST CALCULATIONS BASED ON INPUT RECEIVED
*        OUTPUT:   DATA LINES CONTAINING ALL TRANSACTIONS AS WELL AS
*                  TOTAL AMOUNTS OF DEPOSITS, WITHDRAWALS, ETC.
*
*        REG KEY:
*         7: USED TO CALL SUBROUTINES INSIDE OF SUBROUTINES
*        11: USED TO CALL SUBROUTINES IN MAIN PROGRAM
*
*********************************************************************
*
*        BEGINNING HOUSEKEEPING
         STM   14,12,12(13)
         BALR  12,0
         USING HERE,12
HERE     ST    13,SAVEAREA+4
         LA    13,SAVEAREA
*
*        MAIN DRIVER
*        INITIALIZE COUNTERS AND TOTALS FOR DEPOSITS/WITHDRAWALS
         BAL   11,SETUP
*        THE MAIN LOOP
READLINE XREAD CARD,10
         BC    B'0100',ENDOFILE
         CLI   CARD,C'0'
         BC    B'1000',INITCALL
         CLI   CARD,C'1'
         BC    B'1000',DEPOCALL
         CLI   CARD,C'2'
         BC    B'1000',WDRWCALL
         CLI   CARD,C'3'
         BC    B'1000',INTRCALL
*        CHECK TO SEE IF INIT HAS ALREADY BEEN CALLED
INITCALL CLI   HAVEINIT,C'T'
         BC    B'1000',RESET
         XPRNT CARDOUT,2
         BAL   11,INITIAL
         BC    B'1111',READLINE
DEPOCALL CLI   HAVEINIT,C'T'
         BC    B'0111',READLINE
         XPRNT CARDOUT,2
         BAL   11,DEPOSIT
         BC    B'1111',READLINE
WDRWCALL CLI   HAVEINIT,C'T'
         BC    B'0111',READLINE
         XPRNT CARDOUT,2
         BAL   11,WITHDRAW
         BC    B'1111',READLINE
INTRCALL CLI   HAVEINIT,C'T'
         BC    B'0111',READLINE
         XPRNT CARDOUT,2
         BAL   11,INTEREST
         BC    B'1111',READLINE
*        RESET IS SIMILAR TO ENDOFILE
RESET    BAL   11,PRTFINAL
         XPRNT =C'0CURRENT BALANCE:',17
         ZAP   COSTAREA(5),BALANCE(5)
         BAL   11,PRNTDOLR
*        BUT THEN IT RESETS EVERYTHING
         BAL   11,SETUP
         XPRNT CARDOUT,2
         BAL   11,INITIAL
         BC    B'1111',READLINE
ENDOFILE BAL   11,PRTFINAL
         XPRNT =C'0CURRENT BALANCE:',17
         ZAP   COSTAREA(5),BALANCE(5)
         BAL   11,PRNTDOLR
*
*        END HOUSEKEEPING
         L     13,SAVEAREA+4
         LM    14,12,12(13)
         BR    14
SAVEAREA DS    18F
*
*        DECLARE CONSTANTS AND STORAGE
CARDOUT  DC    C' '
CARD     DS    CL10
TEMP     DS    1F
BALANCE  DS    PL5
PKDINPUT DS    PL5
COSTAREA DS    PL5
MINAREA  DS    PL5
MINTEMP  DS    PL7
RATE     DS    PL2
RATEPRNT DC    C'+       '
RATEOUT  DS    CL5
TOCENTS  DC    P'10000'
OUTPUT   DC    C'+'
OUTAREA  DS    CL12
HAVEINIT DS    C'F'
*        COUNTING AND HOLDING TOTAL DEPOSITS AND WITHDRAWALS
WDCOUNT  DS    PL2
WDTOTAL  DS    PL5
DPCOUNT  DS    PL2
DPTOTAL  DS    PL5
*        OUTPUT FOR DEPOSIT AND WITHDRAWAL TOTALS AND AVERAGES
COUNTPRT DC    C'+'
COUNTOUT DS    CL4
TOTALPRT DC    C'+'
TOTALOUT DS    CL4
AVERAGE  DS    0PL7
QUOTIENT DS    PL5
REMAINDR DS    PL2
*
         EJECT
         TITLE 'SETUP'
*        SETUP
*        PURPOSE: INITIALIZE THE MEMORY LOCATIONS AND PRINTS THE
*        TITLE CARD OF EACH ACCOUNT
SETUP    ZAP   WDCOUNT(2),=P'0'
         ZAP   WDTOTAL(5),=P'0'
         ZAP   DPCOUNT(2),=P'0'
         ZAP   DPTOTAL(5),=P'0'
         ZAP   BALANCE(5),=P'0'
*        PRINT THE HEADING
         XPRNT =C'-      SAVINGS  ACCOUNT  REPORT        ',39
         XPRNT =C' CODE  AMOUNT         RATE   CURRENT BAL',40
         BR    11
*       
         EJECT
         TITLE 'INITIAL'
*        DECLARE SUBROUTINES
*        INITIAL
*        PURPOSE: SETS THE INITAL BALANCE AMOUNT BASED ON INPUT
*        THIS MUST BE CALLED BEFORE ANYTHING ELSE AND ONLY CALLED
*        ONCE
INITIAL  PACK  BALANCE(5),CARD+1(9)
         ZAP   COSTAREA(5),BALANCE(5)
         BAL   7,PRINTVAL
         XPRNT =C'+            ',13
         ZAP   MINAREA(5),BALANCE(5)
         ZAP   COSTAREA(5),BALANCE(5)
         BAL   7,PRINTVAL
*        LET DRIVER KNOW THAT INITIAL VALUE HAS BEEN SET
         MVI   HAVEINIT,C'T'
         BR    11
*
         EJECT
         TITLE 'DEPOSIT'
*        DEPOSIT
*        PURPOSE: ADDS INPUT AMOUNT TO BALANCE AND PRINTS IT OUT
DEPOSIT  PACK  PKDINPUT(5),CARD+1(9)
         ZAP   COSTAREA(5),PKDINPUT(5)
         BAL   7,PRINTVAL
         XPRNT =C'+            ',13
         AP    BALANCE(5),PKDINPUT(5)
*        PRINT 'EM
         ZAP   COSTAREA(5),BALANCE(5)
         BAL   7,PRINTVAL
*        ADD INPUT AND COUNT THIS AS A DEPOSIT
         AP    DPTOTAL(5),PKDINPUT(5)
         AP    DPCOUNT(2),=P'1'
         BR    11
*
         EJECT
         TITLE 'WITHDRAW'
*        WITHDRAW
*        PURPOSE: SUBTRACTS INPUT AMOUNT FROM BALANCE AND PRINTS IT
*        OUT
WITHDRAW PACK  PKDINPUT(5),CARD+1(9)
         ZAP   COSTAREA(5),PKDINPUT(5)
         BAL   7,PRINTVAL
         XPRNT =C'+            ',13
         SP    BALANCE(5),PKDINPUT(5)
*        PRINT 'EM
         ZAP   COSTAREA(5),BALANCE(5)
         BAL   7,PRINTVAL
*        FIND THE MINIMUM VALUE
         BAL   7,MINIMUM
*        COUNT THE AMOUNT AND ADD IT TO PRINT LATER FOR TOTAL
         AP    WDTOTAL(5),PKDINPUT(5)
         AP    WDCOUNT(2),=P'1'
         BR    11
*
         EJECT
         TITLE 'MINIMUM'
*        MINIMUM
*        PURPOSE: FIGURES OUT IF THE CURRENT BALANCE IS THE MINIMUM
*        VALUE
MINIMUM  CP    BALANCE(5),MINAREA(5)
         BC    B'1011',MINEND
         ZAP   MINAREA(5),BALANCE(5)
MINEND   BR    7
*
         EJECT
         TITLE 'INTEREST'
*        INTEREST
*        PURPOSE: CALCULATES THE INTEREST BY MULTIPLYING THE RATE BY
*        THE MINIMUM VALUE CURRENTLY STORED AND ADDING IT TO THE
*        CURRENT BALANCE
INTEREST XPRNT =C'+            ',13
         PACK  RATE(2),CARD+1(3)
*        PRINT THE INTEREST RATE
         BAL   7,PRNTINTR
*        WE WOULDN'T WANT TO DIVIDE BY ZERO ;)
         CP    RATE(2),=P'0'
         BC    B'1000',INTREND
         ZAP   MINTEMP(7),MINAREA(5)
         MP    MINTEMP(7),RATE(2)
         DP    MINTEMP(7),TOCENTS(3)
         AP    BALANCE(5),MINTEMP(4)
INTREND  ZAP   COSTAREA(5),BALANCE(5)
         BAL   7,PRINTVAL
         BR    11
* 
         EJECT
         TITLE 'PRINTVAL'
*        PRINTVAL
*        PURPOSE: PRINTS OUT A NUMERIC DOLLAR VALUE
PRINTVAL MVC   OUTAREA(12),=X'40202020202021204B202060'
         ED    OUTAREA(12),COSTAREA
         XPRNT OUTPUT,13
         BR    7
*
         EJECT
         TITLE 'PRNTDOLR'
*        PRNTDOLR
*        PURPOSE: DOES THE SAME THING THAT PRINTVAL DOES EXCEPT
*        WITH A DOLLAR SIGN IN FRONT
PRNTDOLR LA    1,OUTAREA+6
         MVC   OUTAREA(12),=X'40202020202021204B202060'
         EDMK  OUTAREA(12),COSTAREA
         BCTR  1,0
         MVI   0(1),C'$'
         XPRNT OUTPUT,13
         BR    7
*
         EJECT
         TITLE 'PRNTINTR'
*        PRNTINTR
*        PURPOSE: PRINTS THE INTEREST RATE
PRNTINTR MVC   RATEOUT(5),=X'40214B2020'
         ED    RATEOUT(5),RATE
         XPRNT RATEPRNT,13
         BR    7
*
         EJECT
         TITLE 'PRTFINAL'
*        PRTFINAL
*        PURPOSE: PRINTS TOTAL & AVERAGE DEPOSITS AND WITHDRAWALS
*        FIRST PRINT THE NUMBER OF DEPOSITS
PRTFINAL MVC   COUNTOUT(4),=X'40202120'
         ED    COUNTOUT(4),DPCOUNT
         XPRNT =C'0DEPOSITS: ',11
         XPRNT COUNTPRT,5
*        PRINT DEPOSIT AMOUNT
         XPRNT =C' DEPOSIT AMOUNT: ',17
         ZAP   COSTAREA(5),DPTOTAL(5)
         BAL   7,PRNTDOLR
*        MAKE SURE THAT THE COUNT ISN'T 0
         CP    DPCOUNT(2),=P'0'
         BC    B'1000',SKIPTOWD
*        FIND AVERAGES AND PRINT IT OUT
         ZAP   AVERAGE(7),DPTOTAL(5)
         DP    AVERAGE(7),DPCOUNT(2)
         XPRNT =C' DEPOSIT AVERAGE: ',18
         ZAP   COSTAREA(5),AVERAGE(5)
         BAL   7,PRNTDOLR
*        PRINT THE NUMBER OF WITHDRAWALS
SKIPTOWD MVC   COUNTOUT(4),=X'40202120'
         ED    COUNTOUT(4),WDCOUNT
         XPRNT =C'0WITHDRAWALS: ',14
         XPRNT COUNTPRT,5
*        PRINT WITHDRAW AMOUNT
         XPRNT =C' WITHDRAW AMOUNT: ',18
         ZAP   COSTAREA(5),WDTOTAL(5)
         BAL   7,PRNTDOLR
*        MAKE SURE THAT THE WDCOUNT IS NOT ZERO
         CP    WDCOUNT(2),=P'0'
         BC    B'1000',SKIPTOED
*        FIND AVERAGES AND PRINT IT OUT
         ZAP   AVERAGE(7),WDTOTAL(5)
         DP    AVERAGE(7),WDCOUNT(2)
         XPRNT =C' WITHDRAW AVERAGE: ',19
         ZAP   COSTAREA(5),AVERAGE(5)
         BAL   7,PRNTDOLR
SKIPTOED BR    11
*
*        END OF LINE
         END   COLE7
$ENTRY NUMBERS.TXT
